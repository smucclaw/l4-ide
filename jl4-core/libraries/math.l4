IMPORT prelude

-- Lightweight math helpers that complement the stock prelude.
-- Extend this file as additional OPM numeric functions (Ln, Log, etc.)
-- become required by downstream translators such as opm2l4.

DECLARE EULER IS A NUMBER
EULER MEANS 2.718281828459045

GIVEN x IS A NUMBER
GIVETH A NUMBER
exp x MEANS EXPONENT EULER x

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
ln x MEANS
  IF x LESS THAN OR EQUALS 0 THEN NOTHING ELSE JUST (LN x)

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
log10 x MEANS
  IF x LESS THAN OR EQUALS 0 THEN NOTHING ELSE JUST (LOG10 x)

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
sqrt x MEANS
  IF x LESS THAN 0 THEN NOTHING ELSE JUST (SQRT x)

DECLARE TAN_EPSILON IS A NUMBER
TAN_EPSILON MEANS 0.000000001

GIVEN value IS A NUMBER
GIVETH A NUMBER
absNumber value MEANS
  IF value LESS THAN 0 THEN 0 MINUS value ELSE value

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
sin x MEANS JUST (SIN x)

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
cos x MEANS JUST (COS x)

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
tan x MEANS
  IF absNumber (COS x) LESS THAN TAN_EPSILON
    THEN NOTHING
    ELSE JUST (TAN x)

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
asin x MEANS
  IF x LESS THAN 0 MINUS 1
    THEN NOTHING
    ELSE IF x GREATER THAN 1 THEN NOTHING ELSE JUST (ASIN x)

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
acos x MEANS
  IF x LESS THAN 0 MINUS 1
    THEN NOTHING
    ELSE IF x GREATER THAN 1 THEN NOTHING ELSE JUST (ACOS x)

GIVEN x IS A NUMBER
GIVETH A MAYBE NUMBER
atan x MEANS JUST (ATAN x)
