
-- An authorised person may issue a community protection notice to an
-- individual aged 16 or over, or a body, if satisfied on reasonable grounds thatâ€”
--
--   a. the conduct of the individual or body is having a detrimental effect,
--      of a persistent or continuing nature, on the quality of life of those
--      in the locality, and
--   b. the conduct is unreasonable.

ASSUME Person IS A TYPE
ASSUME `is authorised`  IS A FUNCTION FROM Person TO BOOLEAN

ASSUME Receiver IS A TYPE
ASSUME `is an individual aged 16 or over` IS A FUNCTION FROM Receiver TO BOOLEAN
ASSUME `is a body` IS A FUNCTION FROM Receiver TO BOOLEAN

ASSUME Effect IS A TYPE
ASSUME EffectTarget IS A TYPE

ASSUME `is detrimental` IS A FUNCTION FROM Effect TO BOOLEAN
ASSUME `is of a persistent or continuing nature` IS A FUNCTION FROM Effect TO BOOLEAN
ASSUME `affects the quality of life of those in the locality` IS A FUNCTION FROM Effect TO BOOLEAN

ASSUME Conduct IS A TYPE
ASSUME conduct IS A FUNCTION FROM Receiver TO Conduct
ASSUME effect IS A FUNCTION FROM Conduct TO Effect
ASSUME `is unreasonable` IS A FUNCTION FROM Conduct TO BOOLEAN

GIVEN person     IS A Person      -- person issuing the notice
      receiver   IS A Receiver    -- target of the notice
DECIDE `may issue a community protection notice` IF
      `is authorised` person
  AND    `is an individual aged 16 or over` receiver
      OR `is a body` receiver
  AND -- it is satisfied on reasonable grounds that
          -- a.
              `is detrimental`                                       OF `the effect`
          AND `is of a persistent or continuing nature`              OF `the effect`
          AND `affects the quality of life of those in the locality` OF `the effect`
      AND -- b.
          `is unreasonable` OF `the conduct`
  WHERE
    `the conduct` MEANS conduct OF receiver
    `the effect`  MEANS effect OF `the conduct`
