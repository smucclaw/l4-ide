§ `Assumptions`

ASSUME Person IS A TYPE
ASSUME `mother of` IS A FUNCTION FROM Person TO Person
ASSUME `father of` IS A FUNCTION FROM Person TO Person

ASSUME `is born in the United Kingdom after commencement` IS A FUNCTION FROM Person TO BOOLEAN
ASSUME `is born in a qualifying territory after the appointed day` IS A FUNCTION FROM Person TO BOOLEAN
ASSUME `is settled in the United Kingdom` IS A FUNCTION FROM Person TO BOOLEAN
ASSUME `is settled in the qualifying territory in which the person is born` IS A FUNCTION FROM Person TO BOOLEAN

§ `Improved Readability`
DECLARE Place
  HAS `English name` IS A STRING

`British Overseas Territories` MEANS
  LIST Place OF "Anguilla"
       ^     ^  "Bermuda"
       ^     ^  "British Antarctic Territory"
       ^     ^  "British Indian Ocean Territory"
       ^     ^  "Cayman Islands"
       ^     ^  "Falkland Islands"
       ^     ^  "Gibraltar"
       ^     ^  "Hong Kong"
       ^     ^  "Montserrat"
       ^     ^  "Pitcairn, Henderson, Ducie and Oeno Islands"
       ^     ^  "St Helena, Ascension and Tristan da Cunha"
       ^     ^  "South Georgia and the South Sandwich Islands"
       ^     ^  "The Sovereign Base Areas of Akrotiri and Dhekelia"
       ^     ^  "Turks and Caicos Islands"
       ^     ^  "Virgin Islands"

GIVEN p IS A Place
GIVETH A BOOLEAN
isBOT p MEANS  p's `English name` = "Anguilla"
            OR ^^  ^              = "Bermuda"
            ^  ^^  ^              = "British Antarctic Territory"
            ^  ^^  ^              = "British Indian Ocean Territory"
            ^  ^^  ^              = "Cayman Islands"
            ^  ^^  ^              = "Falkland Islands"
            ^  ^^  ^              = "Gibraltar"
            ^  ^^  ^              = "Hong Kong"
            ^  ^^  ^              = "Montserrat"
            ^  ^^  ^              = "Pitcairn, Henderson, Ducie and Oeno Islands"
            ^  ^^  ^              = "St Helena, Ascension and Tristan da Cunha"
            ^  ^^  ^              = "Sovereign Base Areas of Akrotiri and Dhekelia"
            ^  ^^  ^              = "South Georgia and the South Sandwich Islands"
            ^  ^^  ^              = "Turks and Caicos Islands"
            ^  ^^  ^              = "Virgin Islands"

GIVETH A Place
DECIDE akdh IS (Place WITH `English name` IS "Sovereign Base Areas of Akrotiri and Dhekelia")

#EVAL isBOT akdh

DECLARE Date
  HAS year    IS A NUMBER
      month   ^  ^ ^
      day     ^  ^ ^

commencement    MEANS Date OF 1983, 1, 1
`appointed day` MEANS Date OF 2002, 2, 21

GIVEN p IS A Place
GIVETH A BOOLEAN
DECIDE `qualifying territory` p IS
           isBOT p
   AND NOT p's `English name` = "Sovereign Base Areas of Akrotiri and Dhekelia"

#EVAL `qualifying territory` (Place WITH `English name` IS "Anguilla")

DECLARE Maybe a
  IS ONE OF
    Nothing
    Just HAS val IS AN a

DECLARE NaturalPerson
  HAS mama        IS A Maybe Person
      papa        IS A Maybe Person
      birthPlace  ^  ^ Maybe Place
      birthDate   ^  ^ Maybe Date

GIVEN d IS A Date -- date of birth
      c IS A Date -- commencement
GIVETH A BOOLEAN
DECIDE `after` IF
         ((year OF c) - 1900) * 365 + (month OF c) * 30   + (day OF c)
    <    (d's year    - 1900) * 365 + (d's month   * 30)  + (d's day)

GIVEN p IS A NaturalPerson
GIVETH A BOOLEAN
DECIDE isBornInUK p IF "UK" EQUALS `English name` OF p's birthPlace's val
DECIDE isBornInUS p IF "US" EQUALS `English name` OF p's birthPlace's val
DECIDE afterCommencement p IF `after` (p's birthDate's val) commencement
DECIDE afterAppointedDay p IF `after` (p's birthDate's val) `appointed day`

GIVEN a IS A NUMBER
      b IS A NUMBER
`plus` a b MEANS a + b

§ `Expanded version`

GIVEN p IS A Person
GIVETH A BOOLEAN
DECIDE `is a British citizen` IF
         `is born in the United Kingdom after commencement` p
      OR `is born in a qualifying territory after the appointed day` p
  AND -- when the person is born ...
            `is a British citizen` OF `father of` p
         OR `is a British citizen` OF `mother of` p
      OR    `is settled in the United Kingdom` OF `father of` p
         OR `is settled in the United Kingdom` OF `mother of` p
      OR    `is settled in the qualifying territory in which the person is born` OF `father of` p
         OR `is settled in the qualifying territory in which the person is born` OF `mother of` p

§ `Version using a local auxiliary declaration`

GIVEN p IS A Person
GIVETH A BOOLEAN
DECIDE `is a British citizen (local)` IS
         `is born in the United Kingdom after commencement` p
      OR `is born in a qualifying territory after the appointed day` p
  AND -- when the person is born ...
         `father or mother` `is a British citizen`
      OR `father or mother` `is settled in the United Kingdom`
      OR `father or mother` `is settled in the qualifying territory in which the person is born`
      WHERE
        `father or mother` property MEANS
             property OF `father of` p
          OR property OF `mother of` p

§ `Version using a global auxiliary declaration`

`for father or mother of` person property MEANS
      property OF `father of` person
   OR property OF `mother of` person

GIVEN p IS A Person
GIVETH A BOOLEAN
DECIDE `is a British citizen (variant)` IS
         `is born in the United Kingdom after commencement` p
      OR `is born in a qualifying territory after the appointed day` p
  AND -- when the person is born ...
         `for father or mother of` p `is a British citizen (variant)`
      OR `for father or mother of` p `is settled in the United Kingdom`
      OR `for father or mother of` p `is settled in the qualifying territory in which the person is born`
