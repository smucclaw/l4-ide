ยง `Preamble`

-- definitions of Place, Date, and Person belong in a supporting library, but are here for exposition

-- this is how we represent NULLs -- for example in the case of an orphan, we don't know the parents
DECLARE Maybe something
  IS ONE OF
    Unknown
    Known
      HAS value IS A something

DECLARE Date
  HAS year    IS A NUMBER
      month   IS A NUMBER
      day     ^  ^ ^

BNA_commencement    MEANS Date OF 1983, 1, 1  -- of the main British Nationality Act 1981
@ref source: section 1 of British Nationality Act 1981 c.61 @ 2021-07-01
@ref url: https://www.legislation.gov.uk/ukpga/1981/61/section/1

`appointed day` MEANS Date OF 2002, 2, 21 -- from various acts that subsequently updated the British Nationality Act 1981
@ref source: section 1 of British Overseas Territories Act 2002 c.8 @ 2002-02-26
@ref url: https://www.legislation.gov.uk/ukpga/2002/8/section/1

DECLARE Place
  HAS `English name` IS A STRING

GIVEN p IS A Place
GIVETH A BOOLEAN
@ref url: https://www.legislation.gov.uk/ukpga/1981/61/schedule/6
`is British Overseas Territory` p MEANS
      p's `English name` = "Anguilla"
   OR ^^  ^              = "Bermuda"
   ^  ^^  ^              = "British Antarctic Territory"
   ^  ^^  ^              = "British Indian Ocean Territory"
   ^  ^^  ^              = "Cayman Islands"
   ^  ^^  ^              = "Falkland Islands"
   ^  ^^  ^              = "Gibraltar"
   ^  ^^  ^              = "Hong Kong"
   ^  ^^  ^              = "Montserrat"
   ^  ^^  ^              = "Pitcairn, Henderson, Ducie and Oeno Islands"
   ^  ^^  ^              = "St Helena, Ascension and Tristan da Cunha"
   ^  ^^  ^              = "Sovereign Base Areas of Akrotiri and Dhekelia"
   ^  ^^  ^              = "South Georgia and the South Sandwich Islands"
   ^  ^^  ^              = "Turks and Caicos Islands"
   ^  ^^  ^              = "Virgin Islands"

GIVEN p IS A Place
GIVETH A BOOLEAN
DECIDE `is a qualifying territory` p IF
           `is British Overseas Territory` p
   AND NOT (`English name` OF p) = "Sovereign Base Areas of Akrotiri and Dhekelia"
@ref url: https://www.legislation.gov.uk/ukpga/1981/61/section/50#term-qualifying-territory

DECLARE NaturalPerson
  HAS name         IS A   STRING
      birthPlace   ^  ^     Place
      birthDate    ^  ^     Date
      `settled in` IS A     Place
      `known to be British` IS A BOOLEAN


GIVEN d IS A Date -- date of birth
      c IS A Date -- commencement
GIVETH A BOOLEAN
DECIDE `after` IF
         ((year OF c) - 1900) * 365 + (month OF c) * 30   + (day OF c)
    <    (d's year    - 1900) * 365 + (d's month   * 30)  + (d's day)

GIVEN p IS A NaturalPerson
`is born in a qualifying territory` p MEANS `is a qualifying territory` (p's birthPlace)

GIVEN p IS A NaturalPerson
`born after AppointedDay` p          MEANS `after` (p's birthDate) `appointed day`

GIVEN p IS A NaturalPerson
`is born in a qualifying territory after the appointed day` p MEANS
          `is born in a qualifying territory` p
      AND `born after AppointedDay` p

GIVEN p        IS A NaturalPerson
DECIDE `is born in the USA` IF "US" EQUALS `English name` OF p's birthPlace

GIVEN p        IS A NaturalPerson
DECIDE `is born in the UK`  IF             `English name` OF p's birthPlace
                                    EQUALS "UK"

GIVEN parent   IS A NaturalPerson
DECIDE `is settled in the UK` parent IF `English name` OF parent's `settled in`
                                     EQUALS "UK"

GIVEN p        IS A NaturalPerson
DECIDE `is born after commencement` p IF `after` (p's birthDate) BNA_commencement

-- syntactic sugar for the visualization to read more naturally
flip a b MEANS b a
ascertain MEANS flip
is        ^     ^
person    ^     ^

-- we borrow Soren Kierkegaard to act as the negative case for a parent. 
-- This is meant to fit into a data modeling tutorial about the importance of Nulls
-- and handling them correctly using a Maybe type.
GIVETH A NaturalPerson
danish MEANS NaturalPerson WITH     name         IS "Kierkegaard"
                                    birthPlace   IS Place OF "DK"
                                    birthDate    IS Date  OF 1813, 5, 5
                                    `settled in` IS Place OF "DK"
                                    `known to be British` IS FALSE


ยง `S1 BNA possible redraft form`
GIVEN p IS A NaturalPerson
      m ^  ^ ^
      f ^  ^ ^
GIVETH A BOOLEAN
DECIDE `is British citizen`
    -- Sometimes we are simply given a fact, that some parent is a British citizen.
  IF   p's `known to be British`

  OR       person p `is born in the UK`
      AND  person p `is born after commencement`
      AND       `father or mother`   `is settled in the UK`
            OR  `father or mother 2` `is British citizen`

    OR   person p `is born in a qualifying territory after the appointed day`
      AND       `father or mother`   `is settled in the UK`
            OR  `father or mother`   `is settled in the qualifying territory in which the person is born`
            OR  `father or mother 2` `is British citizen`


  WHERE
    `father or mother` someProperty MEANS
          someProperty OF f
       OR ^            OF m

    `father or mother 2` someProperty MEANS
          IF NOT f EQUALS danish THEN someProperty OF f, danish, danish ELSE FALSE
       OR IF NOT m EQUALS danish THEN someProperty OF m, danish, danish ELSE FALSE

    `is settled in the qualifying territory in which the person is born` parent MEANS
                 `is born in a qualifying territory after the appointed day` p
         AND     `English name` OF parent's `settled in`
         EQUALS  `English name` OF p's birthPlace


GIVETH A NaturalPerson
alice MEANS NaturalPerson WITH name                  IS "Alice"
                               birthPlace            IS Place OF "Gibraltar"
                               birthDate             IS Date  OF 2005, 1, 1
                               `settled in`          IS Place OF "Italy"
                               `known to be British` IS FALSE

GIVETH A NaturalPerson
betty MEANS NaturalPerson WITH
    name                  IS "Betty"
    birthPlace            IS Place OF "France"
    birthDate             IS Date OF 1970, 1, 1
    `settled in`          IS Place OF "Gibraltar"
    `known to be British` IS FALSE

GIVETH A NaturalPerson
charlie MEANS NaturalPerson WITH    name   IS "Charlie"
                                    birthPlace   IS Place OF "France"
                                    birthDate    IS Date OF 1966, 1, 1
                                    `settled in` IS Place OF "France"
                                    `known to be British` IS FALSE

{- 

{
  "alice": {
    "name": "Alice",
    "birthPlace": {
      "English name": "Gibraltar"
    },
    "birthDate": {
      "year": 2005,
      "month": 1,
      "day": 1
    },
    "settled in": {
      "English name": "Italy"
    },
    "known to be British": false
  },

  "betty": {
    "name": "Betty",
    "birthPlace": {
      "English name": "France"
    },
    "birthDate": {
      "year": 1970,
      "month": 1,
      "day": 1
    },
    "settled in": {
      "English name": "Gibraltar"
    },
    "known to be British": true
  },

  "charlie": {
    "name": "Charlie",
    "birthPlace": {
      "English name": "France"
    },
    "birthDate": {
      "year": 1966,
      "month": 1,
      "day": 1
    },
    "settled in": {
      "English name": "UK"
    },
    "known to be British": false
  }
}

-}


#EVAL `is British citizen` alice betty charlie
